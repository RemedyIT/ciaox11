/**
 * @file    dds4ccm_base.idl
 * @author  Marcel Smit
 *
 * @copyright Copyright (c) Remedy IT Expertise BV
 */
#ifndef DDS4CCM_BASE_IDL_
#define DDS4CCM_BASE_IDL_

#include "connectors/dds4ccm/idl/dds_rtf2_dcps.idl"
#include "connectors/dds4ccm/idl/dds4ccm_query_filter.idl"

module CCM_DDS
{
  typedef unsigned long DataNumber_t;   // count or index of data
  typedef sequence<DataNumber_t> DataNumberSeq;
  const DataNumber_t UNLIMITED = 0;

  enum AccessStatus {
    FRESH_INFO,
    ALREADY_SEEN
  };

  enum InstanceStatus {
    /// This is the first time that the component sees that instance (the instance is then existing for the component)
    INSTANCE_CREATED,
    /// An existing instance reenters the filter after having been filtered out
    INSTANCE_FILTERED_IN,
    /// An existing instance is modified and stays within the filter
    INSTANCE_UPDATED,
    /// An existing instance just stopped passing the filter
    INSTANCE_FILTERED_OUT,
    /// An instance just stopped existing
    INSTANCE_DELETED
  };

  struct ReadInfo {
    DDS::InstanceHandle_t instance_handle;
    DDS::Time_t source_timestamp;
    AccessStatus access_status;
    InstanceStatus instance_status;
  };
  typedef sequence<ReadInfo> ReadInfoSeq;

  enum ListenerMode {
    NOT_ENABLED,
    ONE_BY_ONE,
    MANY_BY_MANY
  };

  exception AlreadyCreated {
    DataNumberSeq indexes; // of the erroneous
  };

  exception NonExistent{
    DataNumberSeq indexes; // of the erroneous
  };

  exception InternalError{
    DDS::ReturnCode_t error_code;
    DataNumber_t index; // of the erroneous
  };

  exception NonChangeable {
  };

  local interface ContentFilterSetting {
    void set_filter_parameters (in DDS::StringSeq parameters)
      raises (InternalError);
  };
};

#endif /* DDS4CCM_BASE_IDL_ */
