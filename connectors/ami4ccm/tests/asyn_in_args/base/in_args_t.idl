/**
 * @file    in_args_t.idl
 * @author  Marijke Hengstmengel
 *
 * @brief   base of ami4ccm test with in arguments
 *
 * @copyright Copyright (c) Remedy IT Expertise BV
 * Chamber of commerce Rotterdam nr.276339, The Netherlands
 */

#ifndef INTERINARGS_T_IDL
#define INTERINARGS_T_IDL


#pragma ami4ccm interface "InterInArgsT::MyFoo"

#pragma ami4ccm idl "in_args_tA.idl"

module InterInArgsT
{
  const short update_val = 999;

  typedef short short_array[5];

  // Mixed struct
  struct TestTopic {
    string key;
    long x;
    };

  // String struct
  struct TopicString {
    string key;
    string x_str;
    };

  // Array
  struct TopicArray {
    string key;
    short_array x_array;
    };

  typedef TopicArray TestArray[5];

  // Union
  union X_Union switch (short) {
    case 0:
      string x_test;
    case 1:
      long   x_long;
  };

  // Sequence
  struct test {
    short x_test;
    string x_teststr;
    };
  typedef sequence<test> test_seq;

  enum test_enum
  {
    ZERO,
    ONE,
    TWO
  };

  exception InternalError
    {
      long id;
      string error_string;
    };

  // Sender/Receiver interface
  interface MyFoo
    {
      long foo (in string in_str, in long cmd, out string answer)
        raises (InternalError);
      void var_ins (in string in_str,  out string answer, in double cmd)
        raises (InternalError);
      void var_div_ins (in TestTopic test_topic,  in TopicString topic_str,
                        in TestArray topic_arr, out string answer)
        raises (InternalError);
      void var_div2_ins (in X_Union topic_union,  in test_seq seq,
                         out string answer)
        raises (InternalError);
      void enum_in(out string answer, in test_enum in_test)
        raises (InternalError);
    };
};

#endif
